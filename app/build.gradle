apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.triplet.play'

android {
    compileSdkVersion 27
    dataBinding.enabled = true

    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file(System.getenv("SYNCTHING_RELEASE_PLAY_ACCOUNT_CONFIG_FILE") ?: 'keys.json')
        }
    }

    defaultConfig {
        applicationId "net.syncthing.lite"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 21
        versionName "0.3.11"
        multiDexEnabled true
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile = {
                def path = System.getenv("SYNCTHING_LITE_RELEASE_STORE_FILE")
                return (path) ? file(path) : null
            }()
            storePassword System.getenv("SIGNING_PASSWORD") ?: ""
            keyAlias System.getenv("SYNCTHING_LITE_RELEASE_KEY_ALIAS") ?: ""
            keyPassword System.getenv("SIGNING_PASSWORD") ?: ""
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/*'
    }
    dataBinding {
        enabled = true
    }
}


play {
    jsonFile = file(System.getenv("SYNCTHING_RELEASE_PLAY_ACCOUNT_CONFIG_FILE") ?: 'keys.json')
    uploadImages = true
    track = 'production'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-coroutines:$anko_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:preference-v14:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation 'android.arch.lifecycle:extensions:1.1.1'

    implementation(project(':syncthing-client')) {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.slf4j'
        exclude group: 'ch.qos.logback'
    }

    implementation project(':syncthing-discovery')
    implementation project(':syncthing-temp-repository-encryption')
    implementation project(':syncthing-repository-android')

    implementation 'sk.baka.slf4j:slf4j-handroid:1.7.26'
    implementation 'com.google.zxing:android-integration:3.3.0'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.github.apl-devs:appintro:v4.2.3'
    implementation "commons-io:commons-io:$commons_io_version"
}
